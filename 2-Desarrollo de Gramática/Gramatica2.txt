programa -> funciones main
funciones -> funcion funciones
funciones -> ''
funcion -> tipo id ( parametros ) { instrucciones }
main -> void main() { instrucciones }
parametros -> parametro parametros_rest 
parametros -> ''
parametros_rest -> , parametro parametros_rest 
parametros_rest -> ''
parametro -> tipo id
instrucciones -> instruccion instrucciones 
instrucciones -> ''
instruccion -> declaracion 
instruccion -> asignacion 
instruccion -> if 
instruccion -> while 
instruccion -> for 
instruccion -> print 
instruccion -> return
declaracion -> tipo id = exp ; 
declaracion -> tipo id ;
asignacion -> id = exp ;
if -> if ( exp ) { instrucciones } else
else -> else { instrucciones } 
else -> ''
while -> while ( exp ) { instrucciones }
for -> for ( instruccion exp ; asignacion ) { instrucciones }
print -> print ( exp ) ;
return -> return exp ; 
return -> return ;
tipo -> int 
tipo -> float 
tipo -> bool 
tipo -> void
exp -> E
E -> C E'
E' -> || C E' 
E' -> ''
C -> R C'
C' -> & R C' 
C' -> ''
R -> T R'
R' -> = T R' 
R' -> < T R' 
R' -> > T R' 
R' -> <= T R' 
R' -> >= T R' 
R' -> == T R' 
R' -> != T R' 
R' -> ''
T -> F T'
T' -> + F T' 
T' -> - F T' 
T' -> ''
F -> A F'
F' -> * A F' 
F' -> / A F' 
F' -> % A F' 
F' -> ''
A -> ( L ) 
A -> ( ) 
A -> id B 
A -> num 
A -> true 
A -> false
L -> E L'
L' -> , E L' 
L' -> ''
B -> ( L ) 
B -> ''
